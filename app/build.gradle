apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 28
    buildToolsVersion '28.0.3'
    defaultConfig {
        applicationId "com.devband.tronwalletforandroid"
        minSdkVersion 15
        targetSdkVersion 28
        versionCode 19
        versionName "1.5.3"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }
    dexOptions {
        javaMaxHeapSize "8g"
    }

    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning', 'HardcodedText', 'InvalidPackage'
        textReport true
        textOutput "stdout"
        abortOnError false
        disable 'RestrictedApi'
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            proto {
                srcDir 'src/main/protos'
            }
            java {
                srcDir 'src/main/java'
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.11.0'
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

ext {
    appcompatVersion = '28.0.0'
    constraintLayoutVersion = '1.1.3'
    daggerVersion = '2.16'
    roomVersion = '1.1.1'
    grpcVersion = '1.11.0'
    retrofitVersion = '2.4.0'
    lombokVersion = '1.16.20'
    butterknifeVersion = '8.8.1'
    powermockVersion = '2.0.0-beta.5'
    spongycastleVersion = '1.53.0.0'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':tronlib')

    // support
    implementation "com.android.support:appcompat-v7:$appcompatVersion"
    implementation "com.android.support:design:$appcompatVersion"
    implementation "com.android.support:recyclerview-v7:$appcompatVersion"
    implementation "com.android.support:support-annotations:$appcompatVersion"
    implementation "com.android.support:cardview-v7:$appcompatVersion"
    implementation "com.android.support:support-v4:$appcompatVersion"
    implementation "com.android.support:support-compat:$appcompatVersion"
    implementation "com.android.support.constraint:constraint-layout:$constraintLayoutVersion"

    // ui
    implementation "com.afollestad.material-dialogs:core:0.9.6.0"
    implementation "com.github.medyo:android-about-page:1.2.4"
    implementation "com.timehop.stickyheadersrecyclerview:library:0.4.3@aar"
    implementation "com.akexorcist:RoundCornerProgressBar:2.0.3"
    implementation "com.thefinestartist:finestwebview:1.2.7"
    implementation "com.miguelcatalan:materialsearchview:1.4.0"

    implementation("com.mikepenz:aboutlibraries:6.0.8@aar") {
        transitive = true
    }

    // butterknife
    implementation "com.jakewharton:butterknife:$butterknifeVersion"
    annotationProcessor "com.jakewharton:butterknife-compiler:$butterknifeVersion"

    implementation 'com.jakewharton.timber:timber:4.7.1'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'
    implementation 'com.jakewharton.rxbinding2:rxbinding:2.2.0'

    implementation "com.annimon:stream:1.1.8"
    implementation "com.android.support:multidex:1.0.3"

    // lombok
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // Dagger dependencies
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"

    // local db
    implementation "android.arch.persistence.room:runtime:$roomVersion"
    annotationProcessor "android.arch.persistence.room:compiler:$roomVersion"

    // http
    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-jackson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava2:$retrofitVersion"

    // grpc
    implementation "io.grpc:grpc-okhttp:$grpcVersion"
    implementation "io.grpc:grpc-protobuf-lite:$grpcVersion"
    implementation "io.grpc:grpc-stub:$grpcVersion"
    implementation "javax.annotation:javax.annotation-api:1.3.2"

    implementation "com.madgag.spongycastle:core:$spongycastleVersion"
    implementation "com.madgag.spongycastle:prov:$spongycastleVersion"

    // https://mvnrepository.com/artifact/org.mindrot/jbcrypt
    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.4'

    //image loader
    implementation "com.github.bumptech.glide:glide:4.6.1"

    implementation "com.github.kenglxn.QRGen:android:2.2.0"
    implementation "com.github.nisrulz:qreader:2.1.2"

    //chart
    implementation "com.github.PhilJay:MPAndroidChart:v3.0.3"

    implementation("com.crashlytics.sdk.android:crashlytics:2.9.3@aar") {
        transitive = true;
    }

    implementation "com.github.marcoscgdev:FingerAuth:1.0.0"

    // test
    testImplementation "junit:junit:4.12"
    testImplementation "org.mockito:mockito-core:2.23.4"
    testImplementation "org.hamcrest:hamcrest-all:1.3"
    testImplementation "org.powermock:powermock-api-mockito2:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4-common:$powermockVersion"
    testImplementation "org.powermock:powermock-module-junit4:$powermockVersion"
    androidTestImplementation "com.android.support.test:runner:1.0.2"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.2"
}
