apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"
    defaultConfig {
        applicationId "com.devband.tronwalletforandroid"
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true
        javaCompileOptions {
            annotationProcessorOptions {
                includeCompileClasspath false
            }
        }
    }
    dexOptions {
        javaMaxHeapSize "8g"
    }
    buildTypes {
        debug {
            minifyEnabled false
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning', 'HardcodedText', 'InvalidPackage'
        textReport true
        textOutput "stdout"
        abortOnError false
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    sourceSets {
        main {
            proto {
                srcDir 'src/main/protos'
            }
            java {
                srcDir 'src/main/java'
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/LICENSE'
    }
    testOptions {
        unitTests.returnDefaultValues = true
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.5.1-1'
    }
    plugins {
        javalite {
            artifact = "com.google.protobuf:protoc-gen-javalite:3.0.0"
        }
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.11.0' // CURRENT_GRPC_VERSION
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                javalite {}
                grpc {
                    // Options added to --grpc_out
                    option 'lite'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation project(':tronlib')

    // support
    implementation 'com.android.support:appcompat-v7:27.1.1'
    implementation 'com.android.support:design:27.1.1'
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'
    implementation "com.android.support:recyclerview-v7:27.1.1"
    implementation "com.android.support:support-annotations:27.1.1"
    implementation "com.android.support:cardview-v7:27.1.1"

    // ui
    implementation 'com.afollestad.material-dialogs:core:0.9.6.0'
    implementation 'com.github.medyo:android-about-page:1.2.4'
    implementation 'com.github.clans:fab:1.6.4'
    implementation 'com.timehop.stickyheadersrecyclerview:library:0.4.3@aar'
    implementation 'com.akexorcist:RoundCornerProgressBar:2.0.3'
    implementation 'com.thefinestartist:finestwebview:1.2.7'

    implementation('com.mikepenz:aboutlibraries:6.0.8@aar') {
        transitive = true
    }

    // butterknife
    implementation 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    implementation 'io.reactivex.rxjava2:rxandroid:2.0.2'
    implementation 'com.annimon:stream:1.1.8'
    implementation 'com.android.support:multidex:1.0.3'

    // lombok
    compileOnly 'org.projectlombok:lombok:1.16.20'
    annotationProcessor 'org.projectlombok:lombok:1.16.20'

    // local db
    implementation 'android.arch.persistence.room:runtime:1.0.0'
    annotationProcessor 'android.arch.persistence.room:compiler:1.0.0'
    implementation 'android.arch.paging:runtime:1.0.0-rc1'

    // http
    implementation 'com.squareup.retrofit2:retrofit:2.4.0'
    implementation 'com.squareup.retrofit2:converter-jackson:2.4.0'
    implementation 'com.squareup.retrofit2:adapter-rxjava2:2.4.0'

    // grpc
    implementation 'io.grpc:grpc-okhttp:1.11.0'
    implementation 'io.grpc:grpc-protobuf-lite:1.11.0'
    implementation 'io.grpc:grpc-stub:1.11.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    protobuf 'com.google.protobuf:protobuf-java:3.5.1'

    implementation 'com.madgag.spongycastle:core:1.53.0.0'
    implementation 'com.madgag.spongycastle:prov:1.53.0.0'

    implementation 'com.github.kenglxn.QRGen:android:2.2.0'
    implementation 'com.github.nisrulz:qreader:2.1.2'

    // test
    testImplementation 'junit:junit:4.12'
    testImplementation 'org.powermock:powermock-module-junit4:1.6.2'
    testImplementation 'org.powermock:powermock-api-mockito:1.6.2'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'
}
